dnl ==========================================================================
dnl
dnl Autoconf script for XviD
dnl
dnl Copyright(C) 2003-2004 Edouard Gomez <ed.gomez@free.fr>
dnl
dnl ==========================================================================
dnl
dnl Modified By Vincent TORRI
dnl
dnl For Avisynth 3.0
dnl
dnl ==========================================================================

AC_PREREQ([2.50])

AC_INIT([Avisynth], [3.0], [http://forum.doom9.org])
AC_CONFIG_SRCDIR(configure.in)

dnl Do not forget to increase that when needed.
API_MAJOR="3"
API_MINOR="0"
PACKAGE="Avisynth"

dnl NASM version requirement
minimum_nasm_patch_version=34
nasm_prog="nasm"

dnl Default CFLAGS -- Big impact on overall speed
our_cxxflags_defaults="-Wall"
our_cxxflags_defaults="$our_cxxflags_defaults -fstrength-reduce"
our_cxxflags_defaults="$our_cxxflags_defaults -finline-functions"
our_cxxflags_defaults="$our_cxxflags_defaults -freduce-all-givs"
our_cxxflags_defaults="$our_cxxflags_defaults -ffast-math"
our_cxxflags_defaults="$our_cxxflags_defaults -ansi"

dnl Default GEN_SOURCES for gen_source.sh
dnl On Linux, Vfw files should be removed
GEN_SOURCES=

dnl ==========================================================================
dnl Features - configure options
dnl
dnl modified
dnl
dnl ==========================================================================

FEATURES=""

dnl Assembly code
AC_ARG_ENABLE(assembly,
		AC_HELP_STRING([--disable-assembly],
				 [Disable assembly code]),
		 [if test "$enable_assembly" = "no" ; then
		     assembly="no"
		  else
		     if test "$enable_assembly" = "yes" ; then
			assembly="yes"
		     fi
		  fi],
		 [assembly="yes"])

dnl STL library check
AC_ARG_WITH(stl-path,
		AC_HELP_STRING([--with-stl-path=PATH],
		[STL Library headers]),
		[with_stl_arg="yes"],
		[with_stl_arg="no"])

dnl BOOST library check
AC_ARG_WITH(boost-path,
		AC_HELP_STRING([--with-boost-path=PATH],
		[BOOST Library headers]),
		[with_boost_arg="yes"],
		[with_boost_arg="no"])

dnl FREETYPE library check
AC_ARG_WITH(freetype-path,
		AC_HELP_STRING([--with-freetype-path=PATH],
		[FREETYPE Library path]),
		[with_freetype_arg="yes"],
		[with_freetype_arg="no"])

dnl Build as a module not a shared lib on darwin
AC_ARG_ENABLE(macosx_module,
		AC_HELP_STRING([--enable-macosx_module],
				 [Build as a module on MacOS X]),
		 [if test "$enable_macosx_module" = "yes" ; then
		     macosx_module="yes"
		  else 
		     macosx_module="no"
		  fi],
		  [macosx_module="no"])

dnl ==========================================================================
dnl Default install prefix and checks build type
dnl ==========================================================================

AC_PREFIX_DEFAULT("/usr/local")
AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

dnl ==========================================================================
dnl Check for the C compiler (could be passed on command line)
dnl ==========================================================================

dnl
dnl First we test if CFLAGS have been passed on command line
dnl I do that because autoconf defaults (-g -O2) suck and they would kill
dnl performance. To prevent that we define a good defult CFLAGS at the end
dnl of the script if and only if CFLAGS has not been passed on the command
dnl line
dnl
AC_MSG_CHECKING(whether to use default CXXFLAGS)
if test  x"$CXXFLAGS" = x"" ; then
   force_default_cxx_options="yes"
   AC_MSG_RESULT([yes])
else
   force_default_cxx_options="no"
   AC_MSG_RESULT([no])
fi

dnl Now we can safely check for the C++ compiler
AC_PROG_CXX

dnl ==========================================================================
dnl Check for the install program
dnl ==========================================================================

AC_PROG_INSTALL

dnl ==========================================================================
dnl Check for the ranlib program to generate static library index
dnl ==========================================================================

AC_PROG_RANLIB

dnl ==========================================================================
dnl
dnl This part looks for:
dnl
dnl ARCHITECTURE : The platform architecture
dnl                - IA32 for mmx, mmx-ext, mmx2, sse assembly
dnl                - IA64
dnl                - PPC for PowerPC assembly routines
dnl                - GENERIC for plain C sources only
dnl
dnl BUS: Address bus size (in bits)
dnl      - 32
dnl      - 64
dnl
dnl ENDIANNESS: I think you can guess what this thing means :-)
dnl             - LITTLE_ENDIAN
dnl             - BIG_ENDIAN
dnl
dnl ==========================================================================

dnl
dnl Looking what sources have to be compiled according to the CPU type
dnl

ARCHITECTURE=""

AC_MSG_CHECKING([for whether to use assembly code])
if test x"$assembly" = x"yes" ; then
   AC_MSG_RESULT([yes])
   AC_MSG_CHECKING([for architecture type])
   case "$target_cpu" in
	  i[[3456]]86)
	  AC_MSG_RESULT(ia32)
	  	ARCHITECTURE="IA32"
		;;	
		powerpc)
		AC_MSG_RESULT(PowerPC)
		dnl ATM the ppc port is out of date 
		dnl ARCHITECTURE="PPC"
		ARCHITECTURE="GENERIC"
		;;
		ia64)
		AC_MSG_RESULT(ia64)
		ARCHITECTURE="IA64"
		;;
		*)
		AC_MSG_RESULT($target_cpu)
		ARCHITECTURE="GENERIC"
		;;
   esac
else
   AC_MSG_RESULT([no])
   ARCHITECTURE="GENERIC"
fi

dnl
dnl Testing address bus length
dnl
BUS=""

AC_CHECK_SIZEOF([int *])
case "$ac_cv_sizeof_int_p" in
     4)
	BUS="32BIT"
	;;
     8)
	BUS="64BIT"
	;;
     *)
	AC_MSG_ERROR([XviD supports only 32/64 bit architectures])
	;;
esac

dnl
dnl Testing endianness
dnl
ENDIANNESS=""

AC_C_BIGENDIAN(ENDIANNESS="BIG_ENDIAN", ENDIANNESS="LITTLE_ENDIAN")

dnl ==========================================================================
dnl
dnl Check for OS specific variables
dnl    - SHARED_EXTENSION, STATIC_EXTENSION, OBJECT_EXTENSION
dnl
dnl ==========================================================================

AC_MSG_CHECKING(for build extensions)
SHARED_EXTENSION=""
STATIC_EXTENSION=""
OBJECT_EXTENSION=""
case "$target_os" in
     *bsd*|linux*|beos|irix*|solaris*)
	AC_MSG_RESULT([.so .a .o])
	STATIC_EXTENSION="a"
	SHARED_EXTENSION="so"
	OBJECT_EXTENSION="o"
	;;
     [[cC]][[yY]][[gG]][[wW]][[iI]][[nN]]*|mingw32*|mks*)
	AC_MSG_RESULT([.dll .a .obj])
	STATIC_EXTENSION="a"
	SHARED_EXTENSION="dll"
	OBJECT_EXTENSION="obj"
	;;
     darwin*|raphsody*)
	if test x"$macosx_module" = x"yes"; then
	   AC_MSG_RESULT([.so .a .o])
	   SHARED_EXTENSION="so"
        else
	   AC_MSG_RESULT([.dynlib .a .o])
	   SHARED_EXTENSION="dylib"
	fi
	STATIC_EXTENSION="a"
	OBJECT_EXTENSION="o"
	;;
     *)
        AC_MSG_RESULT([Unknown OS - Using .so .a .o])
	STATIC_EXTENSION="a"
	SHARED_EXTENSION="so"
	OBJECT_EXTENSION="o"
	;;
esac

dnl ==========================================================================
dnl
dnl Determines best options for CC and LD
dnl  - STATIC_LIB, SHARED_LIB, SPECIFIC_CFLAGS, SPECIFIC_LDLAGS
dnl
dnl
dnl
dnl ==========================================================================

AC_MSG_CHECKING(for platform specific LDFLAGS/CFLAGS)
SPECIFIC_LDFLAGS=""
SPECIFIC_CFLAGS=""
PRE_SHARED_LIB=""
case "$target_os" in
     *bsd*|linux*|irix*|solaris*)
	AC_MSG_RESULT([ok])
	STATIC_LIB="libavisynth.\$(STATIC_EXTENSION)"
	SHARED_LIB="libavisynth.\$(SHARED_EXTENSION).\$(API_MAJOR).\$(API_MINOR)"
	SPECIFIC_LDFLAGS="-pthread -Wl,-soname,libavisynth.\$(SHARED_EXTENSION).\$(API_MAJOR) -shared -lc -lm"
	SPECIFIC_CFLAGS="-O2 -fPIC"
	GEN_SOURCES="/com\\\\/d;/vfw\\\\/d;"
	;;
     [[cC]][[yY]][[gG]][[wW]][[iI]][[nN]]*|mingw32*|mks*)
	AC_MSG_RESULT([ok])
	STATIC_LIB="libavisynth.\$(STATIC_EXTENSION)"
	SHARED_LIB="avisynth.\$(SHARED_EXTENSION)"
	SPECIFIC_LDFLAGS="-lboost_thread-mgw-mt-1_31 -lwinmm -lavcodec -lavformat -lfreetype -lgdi32 -lvfw32 -lstlport_mingw32_static -luuid -lole32 -mno-cygwin -shared -Wl,--dll,--out-implib,\$@.a libavisynth.def"
	SPECIFIC_CFLAGS="-Os -mno-cygwin"
	;;
     darwin*|raphsody*)
	STATIC_LIB="libavisynth.\$(STATIC_EXTENSION)"
	SPECIFIC_CFLAGS="-fPIC -fno-common -no-cpp-precomp"
	if test x"$macosx_module" = x"no"; then
	   AC_MSG_RESULT([dylib options])
	   SHARED_LIB="libavisynth.\$(API_MAJOR).\$(SHARED_EXTENSION)"
	   SPECIFIC_LDFLAGS="-dynamiclib -flat_namespace -compatibility_version \$(API_MAJOR) -current_version \$(API_MAJOR).\$(API_MINOR) -install_name \$(libdir)/\$(SHARED_LIB)"
	else 
	   AC_MSG_RESULT([module options])
 	   PRE_SHARED_LIB="libavisynth.\$(SHARED_EXTENSION)-temp.o"
	   SHARED_LIB="libavisynth.\$(SHARED_EXTENSION).\$(API_MAJOR)"
	   SPECIFIC_LDFLAGS="-r -keep_private_externs -nostdlib && \$(CC) \$(LDFLAGS) \$(PRE_SHARED_LIB) -o libavisynth.\$(SHARED_EXTENSION).\$(API_MAJOR) -bundle -flat_namespace -undefined suppress"
	fi
	GEN_SOURCES="/vfw\\/d;"
	;;
     beos)
	AC_MSG_RESULT([ok])
	STATIC_LIB="libavisynth.\$(STATIC_EXTENSION)"
	SHARED_LIB="libavisynth.\$(SHARED_EXTENSION)"
	SPECIFIC_LDFLAGS="-nostart"
	SPECIFIC_CFLAGS="-fPIC"
	;;
     *)
        AC_MSG_RESULT([Unknown Platform (Using default -shared -lc -lm)])
	STATIC_LIB="libavisynth.\$(STATIC_EXTENSION)"
	SHARED_LIB="libavisynth.\$(SHARED_EXTENSION)"
	SPECIFIC_LDFLAGS=""
	SPECIFIC_CFLAGS=""
	;;
esac

if test x"$PRE_SHARED_LIB" = x; then 
  PRE_SHARED_LIB=$SHARED_LIB
fi

dnl ==========================================================================
dnl
dnl Assembler stuff
dnl  - AS, AFLAGS, ASSEMBLY_EXTENSION, SOURCES
dnl
dnl ==========================================================================

AS=""
AFLAGS=""
ASSEMBLY_EXTENSION=""
GENERIC_SOURCES="SRC_GENERIC"
ASSEMBLY_SOURCES=""

dnl
dnl IA32
dnl

if test "$ARCHITECTURE" = "IA32" ; then

   dnl
   dnl Checking nasm existence
   dnl
   AC_CHECK_PROG([ac_nasm], [$nasm_prog], [yes], [no], , [yes])
   if test "$ac_nasm" = "yes" ; then

      dnl
      dnl Checking nasm patch version
      dnl
      AC_MSG_CHECKING([for nasm patch version])
      nasm_patch=`nasm -r | cut -d '.' -f 3 | cut -d ' ' -f 1`
      if test -z $nasm_patch ; then
        nasm_patch=-1
      fi
      AC_MSG_RESULT([$nasm_patch])


      if test "$nasm_patch" -lt "$minimum_nasm_patch_version" ; then
        AC_MSG_WARN([nasm patch version too old - Compiling generic sources only])
	ARCHITECTURE="GENERIC"
      else

         dnl
         dnl Checking nasm format - win32 or elf
         dnl
         AC_MSG_CHECKING([for nasm object format])
         case "$target_os" in
              *bsd*|linux*|beos|irix*|solaris*)
                  AC_MSG_RESULT([elf])
                  NASM_FORMAT="elf"
		  PREFIX=""
                  ;;
              [[cC]][[yY]][[gG]][[wW]][[iI]][[nN]]*|mingw32*|mks*)
                  AC_MSG_RESULT([win32])
                  NASM_FORMAT="win32"
		  PREFIX="-DPREFIX"
                  ;;
          esac

          AS=nasm
          AFLAGS="-I\$(<D)/ -f $NASM_FORMAT $PREFIX"
          ASSEMBLY_EXTENSION="asm"
          ASSEMBLY_SOURCES="SRC_IA32"
       fi

   else
	AC_MSG_WARN([nasm not found - Compiling generic sources only])
	ARCHITECTURE="GENERIC"
   fi

fi

dnl
dnl PPC
dnl

if test "$ARCHITECTURE" = "PPC" ; then
   AS="\$(CC)"
   AFLAGS="-c"
   ASSEMBLY_EXTENSION="s"
   ASSEMBLY_SOURCES="SRC_PPC"
   AC_MSG_CHECKING([for Altivec support])
   cat > conftest.S << EOF
         .text
         vxor 0,0,0
EOF
   if $CC -c conftest.S 2>/dev/null 1>/dev/null ; then
        AC_MSG_RESULT(yes)
        SPECIFIC_CFLAGS="$SPECIFIC_CFLAGS -DARCH_IS_PPC_ALTIVEC"
        ASSEMBLY_SOURCES="SRC_ALTIVEC"
   else
        AC_MSG_RESULT(no)
   fi
   rm -f conftest.*
fi

dnl
dnl IA64
dnl

if test "$ARCHITECTURE" = "IA64" ; then
   AS="\$(CC)"
   AFLAGS="-c"
   ASSEMBLY_EXTENSION="s"
   ASSEMBLY_SOURCES="SRC_IA64"

   case `basename $CC` in
	*ecc*)
		DCT_IA64_SOURCES="SRC_IA64_IDCT_ECC"

		dnl If the compiler is ecc, then i don't know its options
		dnl fallback to "no options"
		if test "$force_default_cxx_options" = "yes" ; then
		   our_cflags_defaults=""
		fi
		;;
	*)
		DCT_IA64_SOURCES="SRC_IA64_IDCT_GCC"
		;;
   esac

fi

dnl ==========================================================================
dnl Check arguments
dnl ==========================================================================

if test "${with_stl_path+set}" = set && test $with_stl_arg != no; then
  AC_MSG_CHECKING([for stl_config.h])
  saved_CXXFLAGS="$CXXFLAGS"
  CXXFLAGS="$CXXFLAGS -I$with_stl_path"
  AC_LANG([C++])
  AC_TRY_COMPILE([
#include <stl_user_config.h>
   ], 
   [],
   [AC_MSG_RESULT(yes)
    CXXFLAGS="$saved_CXXFLAGS"
    have_stl_path=yes
    our_cxxflags_defaults="$our_cxxflags_defaults  -I$with_stl_path"
    ],
   [AC_MSG_ERROR(STL Library not in this path)
    CXXFLAGS="$saved_CXXFLAGS"
    have_stl_path=no])
fi
dnl AM_CONDITIONAL(HAVE_STL_PATH, test x$have_stl_path = xyes)

if test "${with_boost_path+set}" = set && test $with_boost_arg != no; then
  AC_MSG_CHECKING([for config.hpp])
  saved_CXXFLAGS="$CXXFLAGS"
  CXXFLAGS="$CXXFLAGS -I$with_boost_path/"
  AC_TRY_COMPILE([
#include <boost/config.hpp>
   ], 
   [],
   [AC_MSG_RESULT(yes)
    CXXFLAGS="$saved_CXXFLAGS"
    have_boost_path=yes
    our_cxxflags_defaults="$our_cxxflags_defaults  -I$with_boost_path"
    ],
   [AC_MSG_ERROR(Boost Library not in this path)
    CXXFLAGS="$saved_CXXFLAGS"
    have_boost_path=no])
fi
dnl AM_CONDITIONAL(HAVE_BOOST_PATH, test x$have_boost_path = xyes)

if test "${with_freetype_path+set}" = set && test $with_freetype_arg != no; then
  AC_MSG_CHECKING([for freetype.h])
  saved_CXXFLAGS="$CXXFLAGS"
  CXXFLAGS="$CXXFLAGS -I$with_freetype_path/include/freetype2"
  AC_TRY_COMPILE([
#include <freetype/freetype.h>
   ], 
   [],
   [AC_MSG_RESULT(yes)
    CXXFLAGS="$saved_CXXFLAGS"
    have_freetype_path=yes
    our_cxxflags_defaults="$our_cxxflags_defaults  -I$with_freetype_path/include/freetype2"
    LDFLAGS="$LDFLAGS  -L$with_freetype_path/lib -lfreetype"
    ],
   [AC_MSG_ERROR(Freetype Library not in this path)
    CXXFLAGS="$saved_CXXFLAGS"
    have_freetype_path=no])
fi
dnl AM_CONDITIONAL(HAVE_BFREETYPE, test x$have_freetype = xyes)




dnl ==========================================================================
dnl
dnl Check for header files
dnl
dnl ==========================================================================

AC_CHECK_HEADERS(
	assert.h     \
	string.h     \
	limits.h     \
	malloc.h     \
	math.h       \
	, , AC_MSG_ERROR(Missing header file))

if test "x$with_stl_arg" = "xno"; then
AC_CHECK_HEADER([stl_user_config.h],
                [AC_DEFINE([HAVE_STL])]
		[],
                [AC_MSG_ERROR([STL Library missing. Run configure --help])])
fi

if test "x$with_boost_arg" = "xno"; then
AC_CHECK_HEADER([boost/config.hpp],
                [AC_DEFINE([HAVE_BOOST])]
		[AC_MSG_RESULT([OK])],
                [AC_MSG_ERROR([Boost Library missing. Run configure --help])])
fi

if test "x$with_freetype_arg" = "xno"; then
AC_MSG_CHECKING([for freetype.h])
AC_CHECK_HEADER([freetype/freetype.h],
                [AC_DEFINE([HAVE_FREETYPE])]
		[AC_MSG_RESULT([OK])],
                [AC_MSG_ERROR([Freetype Library missing. Run configure --help])])
fi

function vser
{
  v=$1
  VSTART=`echo $v | awk -F_ '{printf("%s", $1);}'`
  V1=`echo $VSTART | awk -F\. '{printf("%s", $1);}'`
  V2=`echo $VSTART | awk -F\. '{printf("%s", $2);}'`
  V3=`echo $VSTART | awk -F\. '{printf("%s", $3);}'`
  V4="0"
  VEND=`echo $v | awk -F_ '{printf("%s", $2);}'`
  if test -n "$VEND"; then
    V4=`echo $VEND | sed s/pre//`
  fi
  V3=$(( $V3 * 100  ));
  V2=$(( $V2 * 10000  ));
  V1=$(( $V1 * 1000000  ));
  V=$(( $V4 + $V3 + $V2 + $V1 ));
  echo $V
}

dnl ==========================================================================
dnl
dnl Now we can set CXXFLAGS if needed
dnl
dnl ==========================================================================

if test "$force_default_cxx_options" = "yes" ; then
   CXXFLAGS="$our_cxxflags_defaults"
fi

dnl ==========================================================================
dnl
dnl Profiling stuff goes here
dnl  - adds options to SPECIFIC_CFLAGS, SPECIFIC_LDLAGS
dnl  - removes incompatible options from CFLAGS
dnl
dnl ==========================================================================

SPECIFIC_LDFLAGS="$SPECIFIC_LDFLAGS $GNU_PROF_LDFLAGS"
SPECIFIC_CFLAGS="$SPECIFIC_CFLAGS $GNU_PROF_CFLAGS"

if test "$enable_gnuprofile" = "yes" ; then
   CFLAGS=`echo $CFLAGS | sed s/'-fomit-frame-pointer'/''/`
fi

dnl ==========================================================================
dnl
dnl Substitions
dnl
dnl ==========================================================================

AC_SUBST(FEATURES)
AC_SUBST(ARCHITECTURE)
AC_SUBST(BUS)
AC_SUBST(ENDIANNESS)
AC_SUBST(SHARED_EXTENSION)
AC_SUBST(STATIC_EXTENSION)
AC_SUBST(OBJECT_EXTENSION)
AC_SUBST(NASM_FORMAT)
AC_SUBST(AS)
AC_SUBST(AFLAGS)
AC_SUBST(ASSEMBLY_EXTENSION)
AC_SUBST(GENERIC_SOURCES)
AC_SUBST(ASSEMBLY_SOURCES)
AC_SUBST(CXX)
AC_SUBST(LDFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(SPECIFIC_LDFLAGS)
AC_SUBST(SPECIFIC_CFLAGS)
AC_SUBST(DCT_IA64_SOURCES)
AC_SUBST(RANLIB)
AC_SUBST(PACKAGE)
AC_SUBST(API_MAJOR)
AC_SUBST(API_MINOR)
AC_SUBST(STATIC_LIB)
AC_SUBST(PRE_SHARED_LIB)
AC_SUBST(SHARED_LIB)
AC_SUBST(GEN_SOURCES)

dnl ==========================================================================
dnl
dnl Output files
dnl
dnl ==========================================================================

AC_CONFIG_FILES([platform.inc])
AC_CONFIG_FILES(gen_sources.sh, [chmod +x gen_sources.sh && ./gen_sources.sh])

AC_OUTPUT

#####################################################################
## Info

echo
echo "------------------------------------------------------------------------"
echo " $PACKAGE $API_MAJOR.$API_MINOR"
echo "------------------------------------------------------------------------"
echo
echo " Install path......: $prefix"
echo
echo " Compilation.......: make"
echo " Installation......: make install"
echo